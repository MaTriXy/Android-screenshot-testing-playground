plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'io.github.takahirom.roborazzi'
}

android {
    namespace 'com.example.road.to.effective.snapshot.testing.dialogs.roborazzi'
    compileSdk 34

    defaultConfig {
        minSdk 23
        targetSdk 34

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
        }
        debug {
            pseudoLocalesEnabled true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            all {
                // Do not run out of memory when running Roborazzi tests for different api levels
                jvmArgs '-Xmx1g'

                // Enable running tests in parallel
                if (project.hasProperty("parallel")) {
                    maxParallelForks = Runtime.runtime.availableProcessors() / 2
                }

                // Enable hardware rendering to display shadows and elevation. Still experimental
                // Supported only on API 28+
                systemProperty 'robolectric.pixelCopyRenderMode', 'hardware'
            }
        }
    }
}

dependencies {
    implementation project(':dialogs')

    testImplementation 'org.robolectric:robolectric:4.13'
    testImplementation 'io.github.takahirom.roborazzi:roborazzi:1.26.0'
    testImplementation 'com.github.sergio-sastre.AndroidUiTestingUtils:utils:2.5.0'
    testImplementation 'com.github.sergio-sastre.AndroidUiTestingUtils:robolectric:2.5.0'
}
