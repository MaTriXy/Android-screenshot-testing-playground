plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'io.github.takahirom.roborazzi'
}

android {
    namespace 'com.example.road.to.effective.snapshot.testing.lazycolumnscreen.roborazzi'
    compileSdk 34

    defaultConfig {
        testApplicationId "com.example.road.to.effective.snapshot.testing"
        minSdk 23
        targetSdk 34

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
        }
        debug {
            pseudoLocalesEnabled true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        // Enables Jetpack Compose for this module
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.5.14'
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            all {
                // Do not run out of memory when running Roborazzi tests for different api levels
                jvmArgs '-Xmx1g'

                // Enable running tests in parallel
                if (project.hasProperty("parallel")) {
                    maxParallelForks = Runtime.runtime.availableProcessors() / 2
                }

                // Enable hardware rendering to display shadows and elevation. Still experimental
                // Supported only on API 31+
                systemProperty 'robolectric.screenshot.hwrdr.native', 'true'
            }
        }
    }
}

dependencies {
    implementation project(':lazycolumnscreen')

    implementation "androidx.navigation:navigation-compose:2.7.7"

    // Jetpack Compose
    testImplementation platform('androidx.compose:compose-bom:2024.04.00')
    testImplementation "androidx.compose.material:material"

    testImplementation "org.robolectric:robolectric:4.12.1"

    testImplementation 'com.github.sergio-sastre.AndroidUiTestingUtils:utils:2.3.2'
    testImplementation 'com.github.sergio-sastre.AndroidUiTestingUtils:robolectric:2.3.2'
    testImplementation 'com.github.sergio-sastre.AndroidUiTestingUtils:roborazzi:2.3.2'
}
