plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'

    // 1 on-device + 1 JVM screenshot test library
    id 'com.dropbox.dropshots'
    id 'io.github.takahirom.roborazzi'
}

android {
    namespace 'com.example.road.to.effective.snapshot.testing.recyclerviewscreen.sharedtests'
    compileSdk 33

    defaultConfig {
        minSdk 23

        // Warning: For Shot, replace it with testInstrumentationRunner "com.karumi.shot.ShotTestRunner"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    testOptions {
        // Not reliable on every API. Seems to work only from API 25 on.
        // Better to disable through developer settings
        animationsDisabled = true

        // Warning. You can remove this block if using Paparazzi
        unitTests {
            // Necessary for RNG-based libraries like Roborazzi
            includeAndroidResources = true

            all {
                // Do not run out of memory when running Roborazzi tests for different api levels
                jvmArgs '-Xmx1g'

                // Enable RNG for Roborazzi tests
                systemProperty 'robolectric.graphicsMode', 'NATIVE'
            }
        }
    }

    // required for shared tests in this module: on-device + JVM screenshot tests
    sourceSets {
        test {
            java.srcDir 'src/sharedTest/java'
        }
        androidTest {
            java.srcDir 'src/sharedTest/java'
        }
    }
}

dependencies {
    debugImplementation project(path: ':recyclerviewscreen')
    implementation project(':testannotations')

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'

    debugImplementation('com.github.sergio-sastre.AndroidUiTestingUtils:utils:2.2.1')

    // Support for Dropshots
    debugImplementation('com.github.sergio-sastre.AndroidUiTestingUtils:dropshots:2.2.1')

    // Support for Roborazzi
    testImplementation('com.github.sergio-sastre.AndroidUiTestingUtils:robolectric:2.2.1')
    testImplementation('com.github.sergio-sastre.AndroidUiTestingUtils:roborazzi:2.2.1')
    debugImplementation('com.github.sergio-sastre.AndroidUiTestingUtils:mapper-roborazzi:2.2.1')
}
